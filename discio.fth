;  CP/M DISC INTERFACE
;
; Last update:
;
; 881228 - EXTEND's R/W address now initialized with blanks
; 860120 - EXTEND's R/W address now HERE, was Osborne video ram
; 850511 - saved BC' in 'BDOS'
; 850227 - saved index regs. in 'BDOS'
; 840812 - added EXTEND
; 840731 - installed BDOS calls
;
;
; CP/M BDOS CALLS USED (as per Albert van der Horst, HCCH)
;
; R/W reads or writes a sector in the file specified when invoking
; Z80 fig-FORTH (A>Z80FORTH d:filename.ext), using the default FCB.
; More than one disc may be accessed by temporary use of a user de-
; fined FCB.
;
;
;
DEFFCB	.EQU	005CH		;default FCB
;
;	CP/M FUNCTIONS
;
OPNFIL	.EQU	0FH		;open file
CLSFIL	.EQU	10H		;close file
SETDMA	.EQU	1AH		;set DMA address
WRTRND	.EQU	22H		;write random
;
;
;	FORTH variables & constants used in disc interface
;
	.BYTE	83H		;FCB (current FCB address)
	.TEXT	'FCB'
	.WORD	PTSTO-5
FCB:	.WORD	DOCON,DEFFCB
;
	.BYTE	84H		;REC# (returns address of random rec.#)
	.TEXT	'REC#'
	.WORD	FCB-6
RECADR:	.WORD	DOCOL,FCB
	.WORD	LIT,21H
	.WORD	PLUS
	.WORD	SEMIS
;
	.BYTE	83H		;USE
	.TEXT	'USE'
	.WORD	RECADR-7
USE:	.WORD	DOVAR,0		;/ initialised by CLD
;
	.BYTE	84H		;PREV
	.TEXT	'PREV'
	.WORD	USE-6
PREV:	.WORD	DOVAR,0		;/ initialised by CLD
;
	.BYTE	85H		;#BUFF
	.TEXT	'#BUFF'
	.WORD	PREV-07H
NOBUF:	.WORD	DOCON,NBUF
;
	.BYTE	8AH		;DISK-ERROR
	.TEXT	'DISK-ERROR'
	.WORD	NOBUF-08H
DSKERR:	.WORD	DOVAR,0
;
;	DISC INTERFACE HIGH LEVEL ROUTINES
;
	.BYTE	84H		;+BUF
	.TEXT	'+BUF'
	.WORD	DSKERR-0DH
PBUF:	.WORD	DOCOL
	.WORD	LIT,CO
	.WORD	PLUS,DUP
	.WORD	LIMIT,EQUAL
	.WORD	ZBRAN,PBUF1-$
	.WORD	DROP,FIRST
PBUF1:	.WORD	DUP,PREV
	.WORD	AT,SUBB
	.WORD	SEMIS
;
	.BYTE	86H		;UPDATE
	.TEXT	'UPDATE'
	.WORD	PBUF-07H
UPDAT:	.WORD	DOCOL,PREV
	.WORD	AT,AT
	.WORD	LIT,8000H
	.WORD	ORR
	.WORD	PREV,AT
	.WORD	STORE,SEMIS
;
	.BYTE	8DH		;EMPTY-BUFFERS
	.TEXT	'EMPTY-BUFFERS'
	.WORD	UPDAT-9
MTBUF:	.WORD	DOCOL,FIRST
	.WORD	LIMIT,OVER
	.WORD	SUBB,ERASEE
	.WORD	SEMIS
;
	.BYTE	83H		;DR0
	.TEXT	'DR0'
	.WORD	MTBUF-10H
DRZER:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	OFSET,STORE
	.WORD	SEMIS
;
	.BYTE	83H		;DR1
	.TEXT	'DR1'
	.WORD	DRZER-6
DRONE:	.WORD	DOCOL
	.WORD	LIT,1600	;Osborne DD
DRON2:	.WORD	OFSET,STORE
	.WORD	SEMIS
;
	.BYTE	86H		;BUFFER
	.TEXT	'BUFFER'
	.WORD	DRONE-6
BUFFE:	.WORD	DOCOL,USE
	.WORD	AT,DUP
	.WORD	TOR
BUFF1:	.WORD	PBUF		;won't work if single buffer
	.WORD	ZBRAN,BUFF1-$
	.WORD	USE,STORE
	.WORD	RR,AT
	.WORD	ZLESS
	.WORD	ZBRAN,BUFF2-$
	.WORD	RR,TWOP
	.WORD	RR,AT
	.WORD	LIT,7FFFH
	.WORD	ANDD,ZERO
	.WORD	RSLW
BUFF2:	.WORD	RR,STORE
	.WORD	RR,PREV
	.WORD	STORE,FROMR
	.WORD	TWOP,SEMIS
;
	.BYTE	85H		;BLOCK
	.TEXT	'BLOCK'
	.WORD	BUFFE-9
BLOCK:	.WORD	DOCOL,OFSET
	.WORD	AT,PLUS
	.WORD	TOR,PREV
	.WORD	AT,DUP
	.WORD	AT,RR
	.WORD	SUBB
	.WORD	DUP,PLUS
	.WORD	ZBRAN,BLOC1-$
BLOC2:	.WORD	PBUF,ZEQU
	.WORD	ZBRAN,BLOC3-$
	.WORD	DROP,RR
	.WORD	BUFFE,DUP
	.WORD	RR,ONE
	.WORD	RSLW
	.WORD	TWOMIN		;/
BLOC3:	.WORD	DUP,AT
	.WORD	RR,SUBB
	.WORD	DUP,PLUS
	.WORD	ZEQU
	.WORD	ZBRAN,BLOC2-$
	.WORD	DUP,PREV
	.WORD	STORE
BLOC1:	.WORD	FROMR,DROP
	.WORD	TWOP,SEMIS
;
	.BYTE	84H		;BDOS  (CP/M function call)
	.TEXT	'BDOS'
	.WORD	BLOCK-8
BDOS:	.WORD	$+2
	EXX			;SAVE IP
	POP	BC		;(C) <-- (S1)LB = CP/M function code
	POP	DE		;(DE) <-- (S2)  = parameter
	push	ix		;/
	push	iy		;/
	exx
	push	bc		;/ save ip
	exx
	CALL	BDOSS		;return value in A
	exx
	pop	bc		;restore ip
	exx
	pop	iy		;/
	pop	ix		;/
	EXX			;restore IP
	LD	L,A
	LD	H,00H
	JHPUSH			;(S1) <-- (HL) = returned value
;
	.BYTE	83H		;R/W
	.TEXT	'R/W'
	.WORD	BDOS-07H
RSLW:	.WORD	DOCOL
	.WORD	TOR		;store R/W flag
	.WORD	RECADR,STORE
	.WORD	ZERO,RECADR	;set record #
	.WORD	TWOP,CSTOR
	.WORD	LIT,SETDMA
	.WORD	BDOS,DROP	;set DMA address
	.WORD	LIT,WRTRND
	.WORD	FROMR,SUBB	;select READ or WRITE
	.WORD	FCB,SWAP
	.WORD	BDOS		;do it
	.WORD	DSKERR,STORE	;store return code
	.WORD	SEMIS
;
	.BYTE	85H		;FLUSH
	.TEXT	'FLUSH'
	.WORD	RSLW-6
FLUSH:	.WORD	DOCOL
	.WORD	NOBUF,ONEP
	.WORD	ZERO,XDO
FLUS1:	.WORD	ZERO,BUFFE
	.WORD	DROP
	.WORD	XLOOP,FLUS1-$
	.WORD	SEMIS
;
;
	.BYTE	86h			;/ EXTEND
	.TEXT	'EXTEND'
	.WORD	FLUSH-08h
EXTEND:
	.WORD	DOCOL
	.WORD	HERE			;/ fill with b/buf blanks
	.WORD	BBUF
	.WORD	BLANK
	.WORD	LIT
	.WORD	0008h
	.WORD	STAR
	.WORD	ZERO
EXTND1:
	.WORD	ONEP			; begin
	.WORD	HERE			;/ was lit,f000h (Osborne video ram)
	.WORD	OVER
	.WORD	ONE
	.WORD	RSLW
	.WORD	DSKERR
	.WORD	AT
	.WORD	ZBRAN
	.WORD	EXTND1-$		; until
	.WORD	SWAP
	.WORD	OVER
	.WORD	PLUS
	.WORD	SWAP
	.WORD	XDO			; do
EXTND2:
	.WORD	HERE			;/ was lit,f000h (Osborne video ram)
	.WORD	IDO
	.WORD	ZERO
	.WORD	RSLW
	.WORD	XLOOP
	.WORD	EXTND2-$		; loop
	.WORD	FCB
	.WORD	LIT
	.WORD	CLSFIL
	.WORD	BDOS			; close file
	.WORD	DROP			; discard return code
	.WORD	FCB
	.WORD	LIT
	.WORD	OPNFIL
	.WORD	BDOS			; & re-open
	.WORD	DROP			; discard return code
	.WORD	SEMIS
;
;
	.BYTE	84H		;LOAD
	.TEXT	'LOAD'
	.WORD	EXTEND-09H
LOAD:	.WORD	DOCOL,BLK
	.WORD	AT,TOR
	.WORD	INN,AT
	.WORD	TOR,ZERO
	.WORD	INN,STORE
	.WORD	BSCR,STAR
	.WORD	BLK,STORE	;BLK <-- SCR * B/SCR
	.WORD	INTER		;INTERPRET FROM OTHER SCREEN
	.WORD	FROMR,INN
	.WORD	STORE
	.WORD	FROMR,BLK
	.WORD	STORE,SEMIS
;
	.BYTE	0C3H		;-->
	.TEXT	'-->'
	.WORD	LOAD-7
ARROW:	.WORD	DOCOL,QLOAD
	.WORD	ZERO
	.WORD	INN,STORE
	.WORD	BSCR,BLK
	.WORD	AT,OVER
	.WORD	MODD,SUBB
	.WORD	BLK,PSTOR
	.WORD	SEMIS
;
;
;
