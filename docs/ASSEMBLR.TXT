SCR # 17 
  0 ( fig-FORTH 8080 Assembler with Z80 extensions       SCR 1 of 5)
  1 
  2 HEX VOCABULARY ASSEMBLER IMMEDIATE
  3 ' ASSEMBLER CFA ' ;CODE 0A + !
  4 : CODE   ?EXEC CREATE [COMPILE] ASSEMBLER !CSP ; IMMEDIATE
  5 : C;     CURRENT @ CONTEXT ! ?EXEC ?CSP SMUDGE ; IMMEDIATE
  6 : LABEL  ?EXEC 0 VARIABLE  SMUDGE -2 ALLOT
  7          [COMPILE] ASSEMBLER !CSP ; IMMEDIATE
  8 : 8*     DUP + DUP + DUP + ;
  9 
 10 ASSEMBLER DEFINITIONS
 11 -->
 12 
 13 
 14 
 15 

SCR # 18 
  0 ( fig-FORTH 8080 Assembler with Z80 extensions       SCR 2 of 5)
  1 
  2 4 CONSTANT H   5 CONSTANT L   7 CONSTANT A   6 CONSTANT PSW
  3 2 CONSTANT D   3 CONSTANT E   0 CONSTANT B   1 CONSTANT C
  4 6 CONSTANT M   6 CONSTANT SP
  5 
  6 : 1MI   <BUILDS C, DOES> C@ C, ;
  7 : 2MI   <BUILDS C, DOES> C@ + C, ;
  8 : 3MI   <BUILDS C, DOES> C@ SWAP 8* + C, ;
  9 : 4MI   <BUILDS C, DOES> C@ C, C, ;
 10 : 5MI   <BUILDS C, DOES> C@ C, , ;
 11 -->
 12 
 13 
 14 
 15 

SCR # 19 
  0 ( fig-FORTH 8080 Assembler with Z80 extensions       SCR 3 of 5)
  1 
  2 00 1MI NOP   76 1MI HLT   F3 1MI DI    FB 1MI EI    07 1MI RLC
  3 0F 1MI RRC   17 1MI RAL   1F 1MI RAR   E9 1MI PCHL  F9 1MI SPHL
  4 E3 1MI XTHL  EB 1MI XCHG  27 1MI DAA   2F 1MI CMA   37 1MI STC
  5 3F 1MI CMC   08 1MI EXAF  D9 1MI EXX   C0 1MI RNZ   C8 1MI RZ
  6 D0 1MI RNC   D8 1MI RC    E0 1MI RPO   E8 1MI RPE   F0 1MI RP
  7 F8 1MI RM    C9 1MI RET
  8 
  9 80 2MI ADD   88 2MI ADC   90 2MI SUB   98 2MI SBB   A0 2MI ANA
 10 A8 2MI XRA   B0 2MI ORA   B8 2MI CMP
 11 
 12 09 3MI DAD   C1 3MI POP   C5 3MI PUSH  02 3MI STAX  0A 3MI LDAX
 13 04 3MI INR   05 3MI DCR   03 3MI INX   0B 3MI DCX   C7 3MI RST
 14 -->
 15 

SCR # 20 
  0 ( fig-FORTH 8080 Assembler with Z80 extensions       SCR 4 of 5)
  1 
  2 D3 4MI OUT   DB 4MI IN    C6 4MI ADI   CE 4MI ACI   D6 4MI SUI
  3 DE 4MI SBI   E6 4MI ANI   EE 4MI XRI   F6 4MI ORI   FE 4MI CPI
  4 
  5 22 5MI SHLD  2A 5MI LHLD  32 5MI STA   3A 5MI LDA   C4 5MI CNZ
  6 CC 5MI CZ    D4 5MI CNC   DC 5MI CC    E4 5MI CPO   EC 5MI CPE
  7 F4 5MI CP    FC 5MI CM    CD 5MI CALL  C3 5MI JMP
  8 
  9 C2 CONSTANT 0=  D2 CONSTANT CS  E2 CONSTANT PE  F2 CONSTANT 0<
 10 
 11 : NOT   8 + ;            : MOV   8* 40 + + C, ;
 12 : MVI   8* 6 + C, C, ;   : LXI   8* 1+ C, , ;
 13 : PCIX  DD C, E9 C,  ;   : PCIY  FD C, E9 C, ;
 14 -->
 15 

SCR # 21 
  0 ( fig-FORTH 8080 Assembler with Z80 extensions       SCR 5 of 5)
  1 
  2 : ENDIF   2 ?PAIRS HERE SWAP ! ;
  3 : THEN    [COMPILE] ENDIF ;
  4 : IF      C, HERE 0 , 2 ;
  5 : ELSE    2 ?PAIRS C3 IF ROT SWAP ENDIF 2 ;
  6 : BEGIN   HERE 1 ;
  7 : UNTIL   SWAP 1 ?PAIRS C, , ;
  8 : AGAIN   1 ?PAIRS C3 C, , ;
  9 : WHILE   IF 2+ ;
 10 : REPEAT  >R >R AGAIN R> R> 2- ENDIF ;
 11 
 12 FORTH DEFINITIONS DECIMAL
 13 ;S
 14 
 15


